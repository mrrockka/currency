plugins {
    id 'java'
    id 'idea'
    id 'application'
    id 'jvm-test-suite'
    alias(libs.plugins.springBootPlugin)
    alias(libs.plugins.springDepManagementPlugin)
}

group = 'by.mrrockka'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

defaultTasks "clean", "assemble"

tasks.withType(JavaCompile).configureEach {
    options.compilerArgs.addAll([
            '--enable-preview',
            '-Amapstruct.suppressGeneratorTimestamp=true',
            '-Amapstruct.defaultComponentModel=spring',
    ])
    options.release.set(22)
}

dependencies {
    implementation libs.springBootWeb
    implementation libs.springBootJdbc
    implementation libs.postgresql
    implementation libs.liquibase
    implementation libs.hikariCP
    implementation libs.jacksonDataformat
    implementation libs.mapstructCore
    implementation libs.lombokMapstructBinding
    implementation libs.openApi

    compileOnly libs.lombok
    annotationProcessor libs.lombok
    annotationProcessor libs.mapstructProcessor

    testCompileOnly libs.lombok
    testAnnotationProcessor libs.lombok
    testAnnotationProcessor libs.mapstructProcessor

    testImplementation libs.assertjCore
    testImplementation libs.jupiter
    testImplementation libs.mockitoCore
    testImplementation libs.mockitoJupiter
}

test {
    useJUnitPlatform()
}


testing {
    suites {
        configureEach {
            useJUnitJupiter()
            targets {
                all {
                    testTask.configure {
                        jvmArgs('--enable-preview')
                        testLogging {
                            events("passed", "failed")
                        }
                    }
                }
            }
        }

        integrationTest(JvmTestSuite) {
            dependencies {
                implementation libs.springBootWeb
                implementation libs.assertjCore
                implementation libs.springBootTest
                implementation libs.springBootJdbc
                implementation libs.testContainerJupiter
                implementation libs.testContainerPostgresql

                compileOnly libs.lombok
                annotationProcessor libs.lombok
                annotationProcessor libs.mapstructProcessor

                implementation(project())
            }
        }
    }
}

application {
    mainClass = 'by.mrrockka.Application'
}

bootJar {
    enabled = true
    archiveBaseName = "exchange-currencies"
}

check.dependsOn integrationTest